```
title: "FlxTween"
apiPath: tweens/FlxTween.html
```

`FlxTween` позволяет создавать анимации и плавную интерполяцию значений. Вам необходимо только указать стартовые и конечные значения, и `FlxTween` сгенерирует все промежуточные значения между ними. Пример использования `FlxTween` показан в [демо-проекте](http://haxeflixel.com/demos/FlxTween/).

Код ниже демонстрирует создание анимации перемещения объекта `FlxSprite` по экрану:

```haxe
sprite.x = 200;
sprite.y = 200;

FlxTween.tween(sprite, { x: 600, y: 800 }, 2);
```
Первый две строки задают стартовую позицию для спрайта, потому что метод `tween()` принимает за стартовые значения текущие положения объекта.

Первый параметр - объект, с которым вы хотите взаимодействовать (например анимировать), второй - объект, содержащий параметры, которые вы хотите изменить и их требуемое финальное значение. В этом примере мы хотим переместить спрайт в значение 600 по оси x и 800 по оси y. Третий параметр определяет длительность изменения значений в секундах, в данном случае - 2 секунды.

## Отмена твина

Если вы стартанули твин, используя код выше, он будет выполняться, пока конечные значения не будут достигнуты, затем остановится. Если вы хотите контролировать твин, то можно сохранить ссылку на объект `FlxTween`, который возвращается при вызове метода `tween()`.

Код ниже останавливает перемещение спрайта, если игрок нажал пробел:

```haxe
var tween:FlxTween;

public function new()
{
	super();
	// создание твина и сохранение ссылки на него
	tween = FlxTween.tween(sprite, { x:600, y:800 }, 2);
}

override public function update(elapsed:Float)
{
	super.update(elapsed);
	
	if (FlxG.keys.justPressed.SPACE)
		tween.cancel();
}
```

## Параметры твина

Метод `tween()` принимает не обязательный четвертый параметр - опции твина.

Возможные значения:

- `type`: тип твина:

	- **FlxTween.ONESHOT**: останавливается и удаляет себя из основного контейнера, когда твин завершился;
	- **FlxTween.PERSIST**: останавливается, когда твин завершился. В отличие от **ONESHOT**, этот тип твина остается в основном контейнере после завершения. Это значит, что вы можете сохранить ссылку на этот твин и запустить его через `start()` в любое время (не будет работать с **ONESHOT**);
	
	- **FlxTween.LOOPING**: сразу после завершения, стартует заново;
	- **FlxTween.PINGPONG**: проигрывается "туда и обратно". Похоже на **LOOPING**, но выполнение происходит в обратном направлении;
	
	- **FlxTween.BACKWARD**: проигрывается в обратном направлении.
	
- `onComplete`: функция будет вызвана, как только твин завершился. Вызывается каждый раз при завершении одного из циклов (у **LOOPING** и **PINGPONG**). Функция должна принимать параметр `FlxTween` и ничего не возвращать.

- `ease`: опциональная функция изинга. Используется для задания разных фунций изинга. Класс [`FlxEase`](http://api.haxeflixel.com/types/flixel/tweens/FlxEase.html) имеет много статичных методов для изинга. Список ниже демонстрирует все изинги для `FlxEase`. В допополнение к этому, `In` может быть заменен на `Out` или `InOut`, в зависимости от того, где вы хотите применить изинг: в начале анимации, в конце или с обоих сторон.

	- `backIn`
	- `bounceIn`
	- `circIn`
	- `cubeIn`
	- `elasticIn`
	- `expoIn`
	- `quadIn`
	- `quartIn`
	- `quintIn`
	- `sineIn`

- `startDelay`: время до старта твина, в секундах.

- `loopDelay`: время до старта следующего повторения твина, в секундах. Актуально только для **LOOPING** и **PINGPONG**.

Например:

```haxe
public function new()
{
	super();
	// устанавливаем начальные значения для спрайта и стартуем твин
	sprite.x = 200;
	sprite.y = 200;
	FlxTween.tween(sprite, { x: 600, y: 800 }, 2, { type: FlxTween.PINGPONG, ease: FlxEase.quadInOut, onComplete: changeColor, startDelay: 1, loopDelay: 2 });
}

function changeColor(tween:FlxTween):Void
{
	// здесь можно поменять цвет спрайта
}
```

Код выше перемещает спрайт между двумя точками(200|200) и (600|800) плавно ускоряясь и замедляясь. Каждый раз, когда спрайт приходит в конечную точку, его цвет будет меняться. Анимация начинается через 1 секунду, и затем спрайт становится на паузу в каждой конечной точке на 2 секунды.

## Специальные твины

Существует много других методов у `FlxTween`, которые используются в определенных ситуациях:

#### `color()`

Меняет красную, зеленую и синюю составляющие цвета независимо. Обычный твин работал бы непредсказуемо.

Использование: **color**(Sprite : `FlxSprite`, Duration : `Float`, FromColor : `Int`, ToColor : `Int`, ?FromAlpha : `Float`, ?ToAlpha : `Float`, ?Options : `TweenOptions`)

Обратите внимание, что в отличие от метода `tween()`, длительность задается перед значением цвета, и необходимо указать стартовое и финальное значения. Опции твина аналогичный стандартному `tween()`.

#### `angle()`

Метод для изменения угла поворота объекта `FlxSprite`.

Использование: **angle**(Sprite : `FlxSprite`, FromAngle : `Float`, ToAngle : `Float`, Duration : `Float`, ?Options : `TweenOptions`)

#### Движения и пути

Класс `FlxTween` также содержит методы `linearMotion()`, `quadMotion()`, `cubicMotion()` и `circularMotion()`, которые позволяют объекту следовать по прямой, кривым или окружностям.

Методы `linearPath()` и `quadPath()` можно использовать для более длинных путей из массива точек, вместо заданного фиксированного числа точек.

Если вы хотите использовать эти методы, ознакомьтесь с [документацией `FlxTween`](http://api.haxeflixel.com/flixel/tweens/FlxTween.html). 